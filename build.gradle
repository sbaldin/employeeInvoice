import java.time.LocalDate

buildscript {

    ext {
        kotlinVersion = '1.3.61'
        spekVersion = '1.1.5'
        slf4jVersion = '1.7.25'
        log4jVersion = '1.2.17'
        poiVersion = '3.12'
        konfVersion = '0.20.0'
        userHomePath = System.properties["user.home"]
        destinationPath = userHomePath + "/.${rootProject.name}"
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version '1.3.60'
    id "application"
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

group 'com.github.sbaldin'
version '0.3.1'

mainClassName = "com.github.sbaldin.invoicer.ApplicationKt"
defaultTasks 'run'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"

    compile 'com.uchuhimo:konf-core:0.20.0'
    compile 'com.uchuhimo:konf-yaml:0.20.0'
    compile "org.apache.poi:poi:${poiVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi-ooxml-schemas:${poiVersion}"
    compile "org.apache.poi:poi-scratchpad:${poiVersion}"
    compile group: 'org.apache.poi', name: 'ooxml-schemas', version: '1.4'
    // https://mvnrepository.com/artifact/org.freemarker/freemarker
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.20'


    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile group: 'log4j', name: 'log4j', version: '1.2.17'

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    testCompile("org.jetbrains.spek:spek-api:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile("org.jetbrains.spek:spek-data-driven-extension:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

//build.dependsOn shadowJar

jar {
    zip64 true
    manifest {
        attributes "Implementation-Title": "Invoice Generator"
        attributes "Implementation-Version": getArchiveVersion()
        attributes "Main-Class": mainClassName
    }
    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

application {
    applicationDefaultJvmArgs = ['-Xmx500m']

}

run {
    if (project.hasProperty('jargs') && jargs.split(',').length > 0) {
        args(jargs.split(','))
    } else {
        args = ["./build/resources/main/application.yaml"]
    }
}

runShadow {
    if (project.hasProperty('jargs') && jargs.split(',').length > 0) {
        args(jargs.split(','))
    } else {
        def invoiceDate = LocalDate.now()
        args = ["./build/resources/main/application.yaml", "25.${invoiceDate.monthValue}.${invoiceDate.year}"]
    }
}

task copyBinaries(type: Copy) {
    from 'binaries'
    into destinationPath + "/binaries"
}

task copyScripts(type: Copy) {
    from 'scripts'
    into destinationPath + "/scripts"
}

task chmodBinaries {
    dependsOn(copyBinaries, copyScripts)
    doLast {
        exec {
            fileTree(destinationPath + "/binaries").visit { FileVisitDetails details ->
                commandLine destinationPath + "/scripts/chmod.sh"
                println(details.file.path)
                args(details.file.path)
            }
        }
    }
}

task copyJar(type: Copy, dependsOn: build) {
    from 'build/libs'
    include "${rootProject.name}-${project.version}-all.jar"
    into destinationPath

}

task copyAppYaml(type: Copy, dependsOn: build) {
    from 'build/resources/main'
    include "application.yaml"
    rename("application.yaml", "application-invoicer.yaml")
    into destinationPath

}


task installToUserHomeDir {
    dependsOn(copyJar, copyAppYaml, copyBinaries, copyScripts, chmodBinaries)
}